import 'PetriNet.ecore'

context petrinet.PetriNetElement
def: petrinet(): petrinet.PetriNet =
	petrinet.PetriNet.allInstances()
		->select(pn | pn.petriNetElements->includes(self))
		->asSequence()->first()
		
context petrinet.PetriNet
inv nameIsValid('Invalid name format for PetriNet: ' + self.name):
	self.name.matches('^[a-zA-Z_][0-9a-zA-Z_]*$')

context petrinet.Place
inv positiveTokenNumber('The number of token in a place must be positive: ' + self.name):
	self.nbJetons >= 0
inv nonEmptyName('Place name cannot be empty: ' + self.name):
	self.name <> ''
inv uniquePlaceName('Place names must be unique within a net'):
	petrinet.Place.allInstances()->forAll(p1, p2 | p1 <> p2 implies p1.name <> p2.name)
inv nameIsValid('Invalid name format for Place: ' + self.name):
	self.name.matches('^[a-zA-Z_][0-9a-zA-Z_]*$')

context petrinet.Arc
inv positiveWeight('The weight must be positive: ' + self.name):
	self.weight >= 0
inv readArcValidType('A "read" arc should be from a place to a transition.'):
	self.type <> petrinet.ArcKind#READ or self.direction = petrinet.ArcDirection#P2T
inv sameNet('Arc, Place, and Transition must be in the same PetriNet'):
	self.petrinet() = self.place.petrinet() and self.petrinet() = self.transition.petrinet()
inv nameIsValid('Invalid name format for Arc: ' + self.name):
	self.name.matches('(^[a-zA-Z_][0-9a-zA-Z_]*)?$')



context petrinet.Transition
inv nonEmptyName('Transition name cannot be empty: ' + self.name):
	self.name <> ''
inv uniqueTransitionName('Transition names must be unique within a net'):
	petrinet.Transition.allInstances()->forAll(t1, t2 | t1 <> t2 implies t1.name <> t2.name)
inv nameIsValid('Invalid name format for Transition: ' + self.name):
	self.name.matches('^[a-zA-Z_][0-9a-zA-Z_]*$')
